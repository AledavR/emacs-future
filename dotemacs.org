# -*- ispell-dictionary: "en_US"; -*-
#+TITLE: Emacs Config
#+STARTUP: show3levels
#+PROPERTY: header-args :comments link :tangle yes :mkdirp yes :results none :noweb yes

This file recreates the emacs configuration

* ~early-init.el~
:PROPERTIES:
:header-args: :tangle early-init.el
:END:
#+begin_src emacs-lisp
  (setq package-enable-at-startup nil)

  (setq gc-cons-threshold most-positive-fixnum)
  (add-hook 'emacs-startup-hook
            (lambda ()
              (setq gc-cons-threshold (* 16 1024 1024))))
#+end_src

* Package management
:PROPERTIES:
:header-args+: :tangle lib/package-manager.el
:END:

#+begin_src emacs-lisp
  ;; (provide 'package-manager)

  ;; (require 'package)
  ;; (add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/") t)
  ;; (package-initialize)

  (provide 'package-manager)

  (defvar elpaca-installer-version 0.8)

  (defvar elpaca-directory (expand-file-name "elpaca/" user-emacs-directory))

  (defvar elpaca-builds-directory (expand-file-name "builds/" elpaca-directory))
  (defvar elpaca-repos-directory (expand-file-name "repos/" elpaca-directory))
  (defvar elpaca-order '(elpaca :repo "https://github.com/progfolio/elpaca.git"
                                :ref nil
                                :files (:defaults "elpaca-test.el" (:exclude "extensions"))
                                :build (:not elpaca--activate-package)))
  (let* ((repo  (expand-file-name "elpaca/" elpaca-repos-directory))
         (build (expand-file-name "elpaca/" elpaca-builds-directory))
         (order (cdr elpaca-order))
         (default-directory repo))
    (add-to-list 'load-path (if (file-exists-p build) build repo))
    (unless (file-exists-p repo)
      (make-directory repo t)
      (when (< emacs-major-version 28) (require 'subr-x))
      (condition-case-unless-debug err
          (if-let* ((buffer (pop-to-buffer-same-window "*elpaca-bootstrap*"))
                    ((zerop (call-process "git" nil buffer t "clone"
                                          (plist-get order :repo) repo)))
                    ((zerop (call-process "git" nil buffer t "checkout"
                                          (or (plist-get order :ref) "--"))))
                    (emacs (concat invocation-directory invocation-name))
                    ((zerop (call-process emacs nil buffer nil "-Q" "-L" "." "--batch"
                                          "--eval" "(byte-recompile-directory \".\" 0 'force)")))
                    ((require 'elpaca))
                    ((elpaca-generate-autoloads "elpaca" repo)))
              (progn (message "%s" (buffer-string)) (kill-buffer buffer))
            (error "%s" (with-current-buffer buffer (buffer-string))))
        ((error) (warn "%s" err) (delete-directory repo 'recursive))))
    (unless (require 'elpaca-autoloads nil t)
      (require 'elpaca)
      (elpaca-generate-autoloads "elpaca" repo)
      (load "./elpaca-autoloads")))
  (add-hook 'after-init-hook #'elpaca-process-queues)
  (elpaca `(,@elpaca-order))

  (elpaca elpaca-use-package
    (elpaca-use-package-mode))

  (use-package org
    :defer
    :ensure `(org
              :remotes ("tecosaur"
                        :repo "https://code.tecosaur.net/tec/org-mode.git"
                        :branch "dev")
              :files (:defaults ("etc/styles/" "etc/styles/*" "doc/*.texi"))
              :build t
              :pre-build
              (progn
                (with-temp-file "org-version.el"
                  (require 'lisp-mnt)
                  (let ((version
                         (with-temp-buffer
                           (insert-file-contents "lisp/org.el")
                           (lm-header "version")))
                        (git-version
                         (string-trim
                          (with-temp-buffer
                            (call-process "git" nil t nil "rev-parse" "--short" "HEAD")
                            (buffer-string)))))
                    (insert
                     (format "(defun org-release () \"The release version of Org.\" %S)\n" version)
                     (format "(defun org-git-version () \"The truncate git commit hash of Org mode.\" %S)\n" git-version)
                     "(provide 'org-version)\n")))
                (require 'elpaca-menu-org)
                (elpaca-menu-org--build))
              :pin nil))

  (elpaca-wait)

  ;; (defun +elpaca-unload-seq (e)
  ;;   "Unload seq before continuing the elpaca build, then continue to build the recipe E."
  ;;   (and (featurep 'seq) (unload-feature 'seq t))
  ;;   (elpaca--continue-build e))

  ;; (elpaca `(seq :build ,(append (butlast (if (file-exists-p (expand-file-name "seq" elpaca-builds-directory))
  ;;                                           elpaca--pre-built-steps
  ;;                                         elpaca-build-steps))
  ;;                              (list '+elpaca-unload-seq 'elpaca--activate-package))))


  (unless (equal system-type 'gnu/linux)
    (elpaca-no-symlink-mode))
#+end_src

* ~init.el~
:PROPERTIES:
:header-args+: :tangle init.el
:END:

#+begin_src emacs-lisp
  (mapc (lambda(string)
          (add-to-list 'load-path (locate-user-emacs-file string)))
        '("ext" "lib"))


  (require 'package-manager)


  (require 'rca-macros)
  (require 'rca-functions)
  (require 'rca-ui)
  (require 'rca-emacs)
  (require 'rca-org)
  (require 'rca-prog)
  (require 'rca-completion)
  (require 'rca-minibuffer)
  (require 'rca-project)
  (require 'rca-keyboard)
  (require 'rca-tex)
  (require 'rca-tools)
#+end_src

* Macros
:PROPERTIES:
:header-args+: :tangle lib/rca-macros.el
:END:

#+begin_src emacs-lisp
  (provide 'rca-macros)

  (defmacro toggle-p (var)
    "Toggles a boolean variable"
    `(if (booleanp ,var) 
         (setq ,var (not ,var))))
#+end_src

* Custom functions
:PROPERTIES:
:header-args+: :tangle lib/rca-functions.el
:END:

#+begin_src emacs-lisp
  (provide 'rca-functions)
#+end_src

** File manipulation

#+begin_src emacs-lisp
  (defun rc/file-find-config ()
    "Find config file interactively"
    (interactive)
    (find-file (locate-user-emacs-file
                (completing-read "Select config file: " emacs-config-files))))

  (defun rc/file-get-el (dir)
    "Get all elisp files from a directory"
    (directory-files dir nil "^[^.].*el$"))

  (defun rc/find-stow-file ()
    (interactive)
    (find-file
     (completing-read "Select config file: "
                      (directory-files-recursively
                       stow-files ".*" nil
                       (lambda (dir)
                         (not (string-match-p ".*git.*" dir)))))))

  (defun find-file-at-point-other-window ()
    (interactive)
    (let ((ffap-file-finder #'find-file-other-window))
      (find-file-at-point)))


  (defun rc/insert-wallpaper-file ()
    (interactive)
    (insert
     (file-name-sans-extension
      (file-name-nondirectory
       (completing-read "Select wallpaper: "
                        (directory-files-recursively
                         wallpaper-files ".*"))))))

  (defun rc/locate-or-create-directory (dir)
    "Search for a directory and create it if doesn't exists"
    (let ((dir_ (locate-user-emacs-file dir)))
       (when (not (file-directory-p dir_))
         (make-directory dir_)) dir_))

  (defun rc/config-insert-footer ()
    (let ((inhibit-message t))
      (goto-char (point-max))
      (insert "\n;; Local Variables:\n;; eval: (add-hook 'after-save-hook (lambda ()(org-babel-detangle)) nil t)\n;; End:")
      (save-buffer)))
#+end_src

** List manipulation

#+begin_src emacs-lisp
  (defun rc/list-append-str (string list &optional position)
    "Appends a string to each element of a list.
  If POSITION is nil appends to the beginning of each element."
    (mapcar (lambda (element)
              (if position
                  (concat element string)
                (concat string element)))
            list))

  (defun rc/list-merge-sublists (list)
    "Merge all the sublists in a list"
    (let (value)
      (dolist (elt list value)
        (setq value (append value elt)))))

  (defun rc/list-select-random (items)
    "Selects a random element from a list"
    (let* ((size (length items))
           (index (random size)))
      (nth index items)))
#+end_src

** Miscellaneous

#+begin_src emacs-lisp
  (defun rc/number-between (number bot top)
    "Determines if a number is within a range"
    (if (< number top)
        (if (> number bot)
            t nil)
      nil))

  (defun rc/time-is-day ()
    "Determines if the current time is considered day"
    (if (rc/number-between
         (nth 2 (decode-time (current-time)))
         8 18)
        t nil))

  (defun rc/truncate-lines-off ()
    "Command to set truncate-lines to t in mode hooks"
    (setq truncate-lines t))

  (defun rc/export-code-block-for-message (start end)
    "Copy current region and format it to a markdown codeblock"
    (interactive "r")
    (setq code-block (buffer-substring start end))
    (setq code-block-formatted (concat "```\n" code-block "```"))
    (deactivate-mark)
    (kill-new code-block-formatted))

  (defun line-contains? (string)
    (s-contains? string
                 (buffer-substring-no-properties
                  (line-beginning-position)
                  (line-end-position))))

  (defun replace-regexp-in-line (regexp replacement)
    (replace-regexp regexp replacement nil
                    (line-beginning-position)
                    (line-end-position)))

  (defun current-line-empty-p ()
    "Return t if the current line is empty otherwise returns nil"
    (save-excursion
      (beginning-of-line)
      (looking-at-p "[[:blank:]]*$")))

  (defun rc/wrap-in-question-marks ()
    (interactive)
    (let ((inicio (region-beginning))
          (fin (region-end)))
      (save-excursion
        (goto-char inicio)
        (insert "¬ø")
        (goto-char (+ fin 1))
        (when (eq (char-before) ?.) 
          (delete-char -1))
        (insert "?"))))

  (defun rc/org-update-idea ()
    "Adds a timestamp at the end of the current subtree."
    (interactive)
    (org-mark-subtree)
    (exchange-point-and-mark)
    (deactivate-mark)
    (previous-line)
    (open-line 1)
    (newline)
    (insert "UPDATE ")
    (org-insert-timestamp (current-time) t t)
    (insert ": "))

  (defun +diary-schedule-class (start-month start-day end-month end-day year days-of-week)
    (and (diary-block start-month start-day year
                      end-month end-day year)
         (or (cl-some (lambda (p) (= p (calendar-day-of-week date)))
                      days-of-week))))
#+end_src

* Emacs module
:PROPERTIES:
:header-args+: :tangle lib/rca-emacs.el
:END:

#+begin_src emacs-lisp
  (provide 'rca-emacs)

  (use-package emacs
    :ensure nil
    :bind (("C-x C-k C-x C-k" . kill-emacs)
           ("C-x B" . ibuffer)
           ("M-z" . zap-up-to-char)
           ("C-z" . nil)
           ("C-x C-r" . nil)
           ("C-x r v" . view-register)
           ("C-z C-j" . rc/file-find-config)
           ("C-z j" . rc/find-stow-file)
           ("C-x C-z" . nil)
           ("C-x t h" . tab-bar-mode)
           ("M-o" . other-window)
           ("C-c P" . find-file-at-point)
           ("C-x K" . (lambda () (interactive) (kill-buffer (current-buffer))))
           ("C-x C-c" . nil)
           ("C-h h" . nil)
           ("M-`" . nil)
           ("<insert>" . nil)
           ("<menu>" . nil))

    :preface
    (setq history-excluded-filetypes '(".*gz" ".*pdf" "bookmarks" "recentf"
      			             "init.el" ".*gitignore" "early-init.el"
      			             ".*log" ".*png" ".*jpg" ".*mp4" ".*gif"
      			             ".*agenda/.*" ".*mod/.*" ".*lib/.*" ".*ext/.*" ".*_db"))
    (setq temporal-directory
          (locate-user-emacs-file "temporal/"))
    (setq snippets-directory
          (locate-user-emacs-file "snippets/"))
    (setq backup-directory
          (rc/locate-or-create-directory  "saves/"))
    (setq undo-history-directory
          (rc/locate-or-create-directory  "undohist/"))
    (setq recentf-file
          (locate-user-emacs-file  "recentf"))
    (setq emacs-config-files-dirs
          '("" "lib/"))
    (setq stow-files
          (concat (getenv "HOME") "/dotfiles/"))
    (put 'eval 'safe-local-variable #'booleanp)
    :custom
    ;; (initial-buffer-choice t)
    (recentf-save-file recentf-file)
    (initial-scratch-message nil)
    (inhibit-initial-startup-message t)
    (ring-bell-function 'ignore)
    (dired-listing-switches "-alh")
    (column-number-mode t)
    (blink-cursor-mode nil)
    (help-window-select t)
    (use-dialog-box nil)
    (auto-save-default nil)
    (auto-save-interval 200)
    (auto-save-timeout 20)
    (history-length 25)
    (auto-save-list-file-prefix nil)
    (backup-directory-alist `(("." . ,backup-directory)))
    (recentf-exclude history-excluded-filetypes)
    (x-select-enable-clipboard t)
    (read-file-name-completion-ignore-case t)
    (async-shell-command-buffer 'confirm-kill-process)
    (server-client-instructions nil)
    (savehist-additional-variables (list 'register-alist))
    (register-use-preview t)
    :config
    (setq emacs-config-files
          (rc/list-merge-sublists
           (mapcar (lambda (dir)
                     (rc/list-append-str
                      dir (rc/file-get-el
                           (concat user-emacs-directory dir))))
                   emacs-config-files-dirs)))
    (recentf-mode 1)
    (savehist-mode 1)
    (global-auto-revert-mode 1)
    (defalias 'yes-or-no-p 'y-or-n-p)
    (add-hook 'prog-mode-hook 'display-line-numbers-mode)
    (add-hook 'shell-mode-hook 'rc/truncate-lines-off)
    (setq-default custom-file
                  (expand-file-name "custom.el" user-emacs-directory))
    (when (file-exists-p custom-file)
      (load custom-file))
    (when (not (file-exists-p temporal-directory))
      (make-directory temporal-directory))

    ;; Greentext mode
    (setq greentext-font-lock
          '(("^>.*" . 'success)))

    (define-derived-mode greentext-mode text-mode "üçÄ"
      "Major mode for display faces in greentext stories. Derived from `text-mode'."
      (setq font-lock-defaults '(greentext-font-lock))
      (olivetti-mode))

    ;; (add-to-list 'default-frame-alist '(height . 37))
    )

  (use-package calendar
    :ensure nil
    :bind (("<f6> c" . calendar))
    :mode ("diary" . diary-mode)
    :custom
    (diary-file "~/.sync/org_files/agenda/diary")
    (calendar-latitude -12.0)
    (calendar-longitude -77.1)
    (calendar-mark-diary-entries-flag t)
    (calendar-mark-holidays-flag t)
    (holiday-bahai-holidays nil)
    (holiday-bahai-holidays nil)
    (holiday-hebrew-holidays nil)
    (holiday-islamic-holidays nil))

#+end_src

* User interface
:PROPERTIES:
:header-args+: :tangle lib/rca-ui.el
:END:

#+begin_src emacs-lisp
  (provide 'rca-ui)
#+end_src

** User interface general options

#+begin_src emacs-lisp
  (use-package emacs
    :ensure nil
    :init
    ;; (set-face-attribute 'default nil :family "Iosevka Comfy" :height 130)
    ;; (set-face-attribute 'fixed-pitch nil :family "Iosevka Comfy")
    ;; (set-face-attribute 'variable-pitch nil :family "Iosevka Comfy Motion")
    (set-face-attribute 'default nil :family "Aporetic Sans Mono" :height 130)
    (set-face-attribute 'fixed-pitch nil :family "Aporetic Sans Mono")
    (set-face-attribute 'variable-pitch nil :family "Aporetic Serif Mono")
    (set-fontset-font t 'emoji (font-spec :family "Apple Color Emoji") nil 'prepend)
    (set-fontset-font t 'symbol (font-spec :family "Apple Color Emoji") nil 'prepend)
    :custom
    (frame-resize-pixelwise t)
    (modus-themes-italic-constructs t)
    :config
    (scroll-bar-mode -1)
    (tool-bar-mode -1)
    (menu-bar-mode -1)
    (setq-default fill-column 80)
    (setq-default indent-tabs-mode nil)
    (setq-default display-line-numbers-width 3)
    (setq-default display-line-numbers-grow-only t)
    (setq tab-bar-format '(tab-bar-format-history
                           tab-bar-format-tabs-groups
                           tab-bar-separator
                           tab-bar-format-add-tab
                           tab-bar-format-align-right
                           tab-bar-format-global)))
#+end_src

** Buffer display options

#+begin_src emacs-lisp
  (use-package emacs
    :ensure nil
    :config
    (defun org-babel-detangle-no-buffer-pop-up (orig-fun &rest args)
      (save-excursion
        (let ((display-buffer-alist
               '((".*" (display-buffer-no-window) (allow-no-window . t)))))
          (apply orig-fun args))))
    (advice-add 'org-babel-detangle :around #'org-babel-detangle-no-buffer-pop-up)
    (setq display-buffer-alist
          '(
            ((derived-mode . shell-mode)
             (display-buffer-reuse-mode-window
              display-buffer-below-selected)
             (window-height . 12)
             (dedicated . t)
             (window-parameters . ((no-other-window . t)
    			         (mode-line-format . none))))
            ("^\\*\\(Help\\|Info\\|Man\\)"
             (display-buffer-in-side-window)
             (side . right)
             (slot . 0)
             (window-width . 0.40))
            ("\\*\\(Output\\|Register Preview\\).*"
             (display-buffer-reuse-mode-window
              display-buffer-at-bottom)
             (window-height . 10)
             (window-parameters . ((mode-line-format . none))))
            ("\\*\\(Agenda Commands\\|Org Agenda\\|Org Select\\).*"
             (display-buffer-reuse-mode-window
              display-buffer-at-bottom)
             (window-parameters . ((mode-line-format . none))))
            ("\\*compilation\\*"
             (display-buffer-reuse-mode-window
              display-buffer-below-selected)
             (window-height . 12)
             (dedicated . t))
            ("\\*\\(Python\\|vterm\\)\\*"
             (display-buffer-reuse-mode-window
              display-buffer-below-selected)
             (window-height . 20)
             (dedicated . t))
            ("\\*undo-tree\\*"
             (display-buffer-in-side-window)
             (side . right)
             (dedicated . t)
             (window-width . 0.25)))))
#+end_src

** User interface variables

#+begin_src emacs-lisp
  (use-package emacs
    :ensure nil
    :init
    (defcustom wallpaper-files
      (concat (getenv "HOME") "/.sync/pix/wallpaper/")
      "Folder where wallpaper files are stored."
      :type 'directory))
#+end_src

** Diminish

#+begin_src emacs-lisp
  (use-package diminish
    :ensure t
    :config
    (diminish 'which-key-mode nil)
    (diminish 'eldoc-mode nil))
#+end_src

** Themes

#+begin_src emacs-lisp
  (use-package ef-themes
    :ensure t
    :init
    (setq themes
          '((parsee ef-reverie ef-elea-dark)
            (yuuma ef-tritanopia-light ef-rosa)
            (nazrin2 ef-light ef-owl)
            (youmu ef-elea-light ef-elea-dark)
            (satori ef-trio-light ef-trio-dark)))
    (setq theme-character 'youmu)
    :config
    (setq ef-themes-mixed-fonts t)
    (setq ef-themes-headings
          '((0 . (1.6))
            (1 . (1.5))
            (2 . (1.3))
            (agenda-date . (1.3))
            (agenda-structure . (1.8))
            (t . (1.1)))))
#+end_src

** Circadian

#+begin_src emacs-lisp
  (use-package circadian
    :ensure t
    :after (:all ef-themes emacs calendar)
    :hook
    (server-after-make-frame . (lambda () (enable-theme (car custom-enabled-themes))))
    :config
    (let* ((theme-colors (cdr (assoc theme-character themes)))
           (sunrise (car theme-colors))
           (sunset (cdr theme-colors)))
      (setq circadian-themes `((:sunrise . ,sunrise)
                               (:sunset . ,sunset))))
    (circadian-setup))
#+end_src

** Olivetti

#+begin_src emacs-lisp
  (use-package olivetti
    :ensure t
    :hook (Info-mode . olivetti-mode))
#+end_src

** Spacious-Padding

#+begin_src emacs-lisp
  (use-package spacious-padding
    :ensure t
    :config
    (setq-default header-line-format
                  '("%e" mode-line-front-space
                    (:propertize
                     display (min-width (6.0)))
                    "%b" mode-line-end-spaces))

    (setq-default mode-line-format
                  '("%e" mode-line-front-space
                    (:propertize
                     ("" mode-line-mule-info mode-line-client mode-line-modified mode-line-remote
                      mode-line-window-dedicated)
                     display (min-width (6.0)))
                    mode-line-frame-identification "   "
                    mode-line-position (project-mode-line project-mode-line-format)
                    (vc-mode vc-mode) "  " mode-line-modes mode-line-misc-info mode-line-end-spaces))

    (set-face-attribute 'header-line-active nil :inherit 'mode-line-active)
    
    (setq spacious-padding-widths
          '( :internal-border-width 6
             :header-line-width 3
             :mode-line-width 3
             :tab-width 3
             :right-divider-width 20
             :scroll-bar-width 4
             :fringe-width 4))
    ;; (setq spacious-padding-subtle-mode-line
    ;;       `( :mode-line-active 'default
    ;;          :mode-line-inactive vertical-border))

    (spacious-padding-mode 1)

    ;; Set a key binding if you need to toggle spacious padding.
    (define-key global-map (kbd "<f8>") #'spacious-padding-mode))
#+end_src

** Dashboard

#+begin_src emacs-lisp
  (defun my-inhibit-startup-screen-file ()
    "Startup screen inhibitor for `command-line-functions`.
  Inhibits startup screen on the first unrecognised option which
  names an existing file."
    (ignore
     (setq inhibit-startup-screen
  	 (file-exists-p
  	  (expand-file-name argi command-line-default-directory)))))

  ;; (add-hook 'command-line-functions #'my-inhibit-startup-screen-file)
  (setq command-line-functions #'my-inhibit-startup-screen-file)

  (use-package dashboard
    :ensure t
    :preface
    (defun protect-dashboard ()
      (define-key
       dashboard-mode-map (kbd "q") 'dashboard-refresh-buffer))
    (defun rc/refresh-buffer-maybe ()
      (when (equal "*dashboard*" (buffer-name))
        (revert-buffer)))
    ;; Files don't open from command line if this is in init
    ;; TODO Check if there is any other problem in this section
    :init
    (setq banner-images
          (directory-files (locate-user-emacs-file "img") t ".*g$"))
    (setq banner-image-size (if (equal system-name "acer") 500 550))
    :hook
    (elpaca-after-init . dashboard-insert-startupify-lists)
    (elpaca-after-init . dashboard-initialize)
    (dashboard-mode . protect-dashboard)
    (dashboard-after-initialize . dashboard-refresh-buffer)
    (server-after-make-frame . rc/refresh-buffer-maybe)
    ;; (server-after-make-frame . (lambda () (set-frame-font "Aporetic Sans Mono 13")))
    :custom
    (dashboard-center-content t)
    (dashboard-startup-banner `(,(locate-user-emacs-file (concat "img/" (symbol-name theme-character) ".png"))))
    ;; (dashboard-startup-banner `(,(rc/list-select-random banner-images)))
    ;; (dashboard-startup-banner banner-images)
    (dashboard-set-navigator t)
    (dashboard-navigator-buttons `(((nil "Open agenda" "Open detailed agenda buffer" (lambda (&rest _) (org-agenda nil "a"))))))
    (dashboard-image-banner-max-height banner-image-size)
    (dashboard-startupify-list '(dashboard-insert-banner
                                 dashboard-insert-newline
                                 dashboard-insert-banner-title
                                 dashboard-insert-newline
                                 dashboard-insert-navigator
                                 dashboard-insert-init-info
                                 dashboard-insert-items
                                 dashboard-insert-newline
                                 dashboard-insert-footer))
    (dashboard-banner-logo-title nil)
    (dashboard-match-agenda-entry "-class")
    (dashboard-set-footer nil)
    (dashboard-footer-messages (list nil))
    (tab-bar-new-tab-choice "*dashboard*")
    (dashboard-items '((agenda . 10)))
    ;; (dashboard-agenda-tags-format 'ignore)
    :init
    (dashboard-setup-startup-hook)
    (setq initial-buffer-choice
          (lambda () (get-buffer-create "*dashboard*"))))
    ;; :config
    ;; (add-hook server-after-make-frame-hook 'revert-buffer))
#+end_src

** Rainbow mode

#+begin_src emacs-lisp
  (use-package rainbow-mode
    :ensure t
    :defer t)
#+end_src

* Org-mode
:PROPERTIES:
:header-args+: :tangle lib/rca-org.el
:END:

#+begin_src emacs-lisp
  (provide 'rca-org)
#+end_src

** Org general options
#+begin_src emacs-lisp
  (use-package org
    :ensure nil
    :bind (("C-z C-a" . org-agenda)
           :map org-mode-map
           ("C-c C-x 1" . rc/org-update-idea))
    :hook ((org-capture-mode . org-align-tags)
           (org-mode . variable-pitch-mode)
           (org-mode . visual-line-mode)
           (org-agenda-mode . hl-line-mode)
           (org-babel-after-execute . org-redisplay-inline-images)
           (org-babel-after-execute . org-toggle-inline-images))
    :custom
    (org-highlight-latex-and-related '(latex script entities))
    (org-agenda-files '("~/.sync/org_files/agenda/"))
    (org-log-done 'time)
    (org-confirm-babel-evaluate nil)
    (org-agenda-skip-deadline-if-done t)
    (org-agenda-skip-scheduled-if-done t)
    (org-agenda-skip-scheduled-repeats-after-deadline t)
    (org-image-actual-width nil)
    (org-fold-catch-invisible-edits 'show-and-error)
    (org-list-demote-modify-bullet '(("+" . "-") ("-" . "+")))
    (org-agenda-time-grid '((daily today require-timed)
                            (800 1000 1200 1400 1600 1800 2000 2200)
                            "......"
                            "-----------------"))
    (modus-themes-headings '((1 . (1.5)) (2 . (1.3))
                             (agenda-date . (1.3))
                             (agenda-structure . (1.8))
                             (t . (1.1))))
    (org-safe-remote-resources
     '("\\`https://fniessen\\.github\\.io/org-html-themes/org/theme-readtheorg\\.setup\\'"))
    :config
    (setf (cdr (assoc 'file org-link-frame-setup)) 'find-file)
    (org-babel-do-load-languages
     'org-babel-load-languages
     '((python . t)
       (julia . t)
       (shell . t)
       (calc . t)
       (octave . t)))
    (defun +org-link-mpv-complete-file ()
      (let ((file (read-file-name "File: "))
  	  (pwd (file-name-as-directory (expand-file-name ".")))
  	  (pwd1 (file-name-as-directory (abbreviate-file-name
  				         (expand-file-name ".")))))
        (cond ((string-match
                (concat "^" (regexp-quote pwd1) "\\(.+\\)") file)
               (concat "mpv:" (match-string 1 file)))
  	    ((string-match
                (concat "^" (regexp-quote pwd) "\\(.+\\)")
                (expand-file-name file))
               (concat "mpv:" (match-string 1 (expand-file-name file))))
  	    (t (concat "mpv:" file)))))

    (defun +org-link-open-in-mpv (file)
      "Opens linked file in an new mpv process"
      (start-process "open file" nil "mpv" "--title=mpv_emacs" (expand-file-name file)))
    
    (defun +org-link-remote-open-in-mpv (url)
      "Opens linked file in an new mpv process"
      (start-process "open url" nil "mpv" "--title=mpv_emacs" url))

    (defun browse-steam-page (steam-id)
      (browse-url (concat "steam://advertise/" steam-id)))
    (set-face-attribute 'org-latex-and-related nil :family "Aporetic Sans Mono")
    (org-link-set-parameters "steam" :follow 'browse-steam-page)
    (org-link-set-parameters "mpv" :complete '+org-link-mpv-complete-file :follow '+org-link-open-in-mpv)
    (org-link-set-parameters "mpv-url" :follow '+org-link-remote-open-in-mpv)
    )
#+end_src

** Org-capture
#+begin_src emacs-lisp
  (use-package org-capture
    :ensure nil
    :after org
    :bind (("C-z C-c" . org-capture)
           ("C-z C-l" . org-store-link))
    :preface
    (defvar my/org-academic-agenda "~/.sync/org_files/agenda/academic.org")
    (defvar my/org-personal-agenda "~/.sync/org_files/agenda/personal.org")
    (defvar my/org-idea-notebook "~/.sync/org_files/notes/ideas.org")
    (defvar my/org-dream-diary "~/.sync/org_files/notes/dreams.org")
    
    (defvar my/org-created-property
      "\n:PROPERTIES:\n:CREATED: [%<%Y-%m-%d %a %H:%M>]\n:END:")

    (defvar my/org-file-link
      "\n\nArchivo: [[%L][%f]]")
    
    (defun rc/refile-to (file headline)
      "Move current headline to specified location"
      (let ((pos (save-excursion
  		 (find-file file)
  		 (org-find-exact-headline-in-buffer headline))))
        (org-refile nil nil (list headline file nil pos)))
      (org-save-all-org-buffers)
      (switch-to-buffer (current-buffer)))
    
    (defun rc/idea-to-task (class)
      "Promotes an idea to a pending task"
      (interactive
       (list (completing-read "Tipo de tarea:" '("Universidad" "Personal"))))
      (org-todo "TODO")
      (rc/refile-to my/org-personal-agenda class))
    
    :custom
    (org-capture-templates `(
                             ("a" "academic task")
                             ("ae" "exam" entry (file+headline my/org-academic-agenda "Exam"), (concat "* TODO %^{Exam} %^g\nSCHEDULED: %^T" my/org-created-property) :empty-lines 1)
                             ("ap" "project" entry (file+headline my/org-academic-agenda "Project"), (concat "* TODO %^{Project} %^g\nDEADLINE:%^T" my/org-created-property) :empty-lines 1)
                             ("ah" "homework" entry (file+headline my/org-academic-agenda "Homework"), (concat "* TODO %^{Homework} %^g\nDEADLINE:%^T" my/org-created-property) :empty-lines 1)
                             ("p" "personal task")
                             ("pc" "constructive" entry (file+headline my/org-personal-agenda "Constructive"), (concat "* TODO %^{Task}\nDEADLINE: %^T" my/org-created-property) :empty-lines 1)
                             ("pm" "mundane" entry (file+headline my/org-personal-agenda "Mundane"), (concat "* TODO %^{Task}\nDEADLINE: %^T" my/org-created-property) :empty-lines 1)
                             ("n" "note")
                             ("ni" "idea" entry (file my/org-idea-notebook), (concat "* %^{Idea}" my/org-created-property "\n%?") :empty-lines 1)
                             ("nd" "dream" entry (file my/org-dream-diary), (concat"* %^{Dream}" my/org-created-property "\n%?") :empty-lines 1)
                             ("i" "ideas management")
                             ("ic" "make constructive task from idea" entry (file+headline my/org-personal-agenda "Constructive"), (concat "* TODO %a \nDEADLINE %^T" my/org-created-property "\n%?") :empty-lines 1)
                             ("im" "make mundane task from idea" entry (file+headline my/org-personal-agenda "Mundane"), (concat "* TODO %a \nDEADLINE %^T" my/org-created-property "\n%?") :empty-lines 1)
                             ))
    )

#+end_src
  
** Org export options
#+begin_src emacs-lisp
  (use-package org
    :ensure nil
    :config
    ;; Code extracted from
    ;; https://pragmaticemacs.wordpress.com/2017/03/13/export-org-mode-headlines-to-separate-files/
    (defun org-export-headlines-to-pdf ()
      "Export all subtrees that are *not* tagged with :noexport: to
  separate files.

  Subtrees that do not have the :EXPORT_FILE_NAME: property set
  are exported to a filename derived from the headline text."
      (interactive)
      (save-buffer)
      (let ((modifiedp (buffer-modified-p)))
        (save-excursion
          (goto-char (point-min))
          (goto-char (re-search-forward "^*"))
          (set-mark (line-beginning-position))
          (goto-char (point-max))
          (org-map-entries
           (lambda ()
             (let ((export-file (org-entry-get (point) "EXPORT_FILE_NAME")))
               (unless export-file
                 (org-set-property
                  "EXPORT_FILE_NAME"
                  (replace-regexp-in-string " " "_" (nth 4 (org-heading-components)))))
               (deactivate-mark)
               (org-latex-export-to-pdf nil t)
               (unless export-file (org-delete-property "EXPORT_FILE_NAME"))
               (set-buffer-modified-p modifiedp)))
           "-noexport" 'region-start-level)))))
#+end_src

** Org export packages
#+begin_src emacs-lisp
  (use-package htmlize
    :ensure t)

  (use-package ox-pandoc
    :ensure t
    :custom
    (org-pandoc-options
     '((lua-filter . "pagebreak.lua")
       (standalone . t)
       (highlight-style . "tango"))))
#+end_src

** Org latex options
#+begin_src emacs-lisp :tangle no
  (use-package org
    :ensure nil
    :custom
    (org-highlight-latex-and-related '(latex script entities))
    (org-latex-compiler "lualatex")
    (org-pretty-entities-include-sub-superscripts nil)
    :config
    (setq org-latex-listings 'minted
          org-latex-packages-alist '(("" "minted")))
    (setq org-latex-pdf-process
          '("lualatex -shell-escape -interaction nonstopmode %f"
            "lualatex -shell-escape -interaction nonstopmode %f"))
    (setq luamagick '(luamagick :programs ("lualatex" "magick")
                                :description "pdf > png"
                                :message "you need to install lualatex and imagemagick."
                                :use-xcolor t
                                :image-input-type "pdf"
                                :image-output-type "png"
                                :image-size-adjust (1.0 . 1.0)
                                :latex-compiler ("lualatex -interaction nonstopmode -output-directory %o %f")
                                :image-converter ("magick convert -density %D -trim -antialias %f -quality 100 %O")))
    (add-to-list 'org-preview-latex-process-alist luamagick)
    (setq org-preview-latex-default-process 'luamagick)
    (setq org-preview-latex-default-process 'imagemagick)
    )
#+end_src

** Org latex preview
#+begin_src emacs-lisp
  (use-package org-latex-preview
    :config
    ;; Increase preview width
    (plist-put org-latex-preview-appearance-options
               :page-width 0.8)

    ;; Use dvisvgm to generate previews
    ;; You don't need this, it's the default:
    (setq org-latex-preview-process-default 'dvisvgm)
    
    ;; Turn on auto-mode, it's built into Org and much faster/more featured than
    ;; org-fragtog. (Remember to turn off/uninstall org-fragtog.)
    (add-hook 'org-mode-hook 'org-latex-preview-auto-mode)

    ;; Block C-n, C-p etc from opening up previews when using auto-mode
    (setq org-latex-preview-auto-ignored-commands
          '(next-line previous-line mwheel-scroll
                      scroll-up-command scroll-down-command))

    ;; Enable consistent equation numbering
    (setq org-latex-preview-numbered t)

    ;; Bonus: Turn on live previews.  This shows you a live preview of a LaTeX
    ;; fragment and updates the preview in real-time as you edit it.
    ;; To preview only environments, set it to '(block edit-special) instead
    (setq org-latex-preview-live t)

    ;; More immediate live-previews -- the default delay is 1 second
    (setq org-latex-preview-live-debounce 0.25)
    (defun my/org-latex-preview-uncenter (ov)
      (overlay-put ov 'before-string nil))
    (defun my/org-latex-preview-recenter (ov)
      (overlay-put ov 'before-string (overlay-get ov 'justify)))
    (defun my/org-latex-preview-center (ov)
      (save-excursion
        (goto-char (overlay-start ov))
        (when-let* ((elem (org-element-context))
                    ((or (eq (org-element-type elem) 'latex-environment)
                         (string-match-p "^\\\\\\[" (org-element-property :value elem))))
                    (img (overlay-get ov 'display))
                    (prop `(space :align-to (- center (0.55 . ,img))))
                    (justify (propertize " " 'display prop 'face 'default)))
          (overlay-put ov 'justify justify)
          (overlay-put ov 'before-string (overlay-get ov 'justify)))))
    (define-minor-mode org-latex-preview-center-mode
      "Center equations previewed with `org-latex-preview'."
      :global nil
      (if org-latex-preview-center-mode
          (progn
            (add-hook 'org-latex-preview-overlay-open-functions
                      #'my/org-latex-preview-uncenter nil :local)
            (add-hook 'org-latex-preview-overlay-close-functions
                      #'my/org-latex-preview-recenter nil :local)
            (add-hook 'org-latex-preview-overlay-update-functions
                      #'my/org-latex-preview-center nil :local))
        (remove-hook 'org-latex-preview-overlay-close-functions
                     #'my/org-latex-preview-recenter)
        (remove-hook 'org-latex-preview-overlay-update-functions
                     #'my/org-latex-preview-center)
        (remove-hook 'org-latex-preview-overlay-open-functions
                     #'my/org-latex-preview-uncenter))))
#+end_src

* Programming environment
:PROPERTIES:
:header-args+: :tangle lib/rca-prog.el
:END:

#+begin_src emacs-lisp
  (provide 'rca-prog)
#+end_src

** Terminal

#+begin_src emacs-lisp
  (use-package vterm
    :ensure t
    :defer t)
#+end_src

** FORTRAN


#+begin_src emacs-lisp
  (use-package fortran
    :ensure nil
    :config
    (add-hook 'f90-mode-hook
              (lambda ()
                (set (make-local-variable 'compile-command)
                     (format "gfortran %s && ./a.out" (file-name-nondirectory buffer-file-name))))))
#+end_src

** Gnu plot

#+begin_src emacs-lisp
  (use-package gnuplot
    :ensure t
    :defer t)
#+end_src

** LUA

#+begin_src emacs-lisp
  (use-package lua-mode
    :ensure t
    :defer t)

  (use-package lua-ts-mode
    :ensure nil
    ;; :defer t
    :mode "\\.lua\\'"
    :bind (:map lua-ts-mode-map ("C-c C-c" . lua-send-buffer)))
#+end_src

** Julia

#+begin_src emacs-lisp
  (use-package julia-mode
    :ensure t
    :defer t
    :bind (:map julia-mode-map ("`" . julia-insert-unicode-symbol))
    :init  
    (defvar julia-unicode-symbols-alist
      '((?a . "Œ±") (?b . "Œ≤")
        (?\C-a . "‚Çê")
        (?0 . "‚ÇÄ")
        (?1 . "‚ÇÅ")
        (?2 . "‚ÇÇ")
        (?3 . "‚ÇÉ")
        (?4 . "‚ÇÑ"))
      "List of unicode symbols to be inserted in julia-mode")

    (defun julia-insert-unicode-symbol ()
      (interactive)
      (let* ((char (read-char "Insert symbol: "))
             (entry (assoc char julia-unicode-symbols-alist))
             (symbol (cdr entry)))
        (if (equal nil entry)
            (error "The symbol is not mapped")
          (insert symbol)))))

  (use-package julia-snail
    :ensure t
    :defer t
    :hook (julia-mode . julia-snail-mode))
#+end_src

** Python

#+begin_src emacs-lisp
  (use-package python-mode
    :ensure nil
    :defer t
    :bind (:map python-mode-map
                ("C-c v" . python-set-venv))
    :init
    (defun python-set-venv (interpreter)
      (interactive "fPython interpreter:")
      (setq python-interpreter interpreter
            python-shell-interpreter interpreter))
    :config
    (setq-default python-eldoc-get-doc nil))
#+end_src

** Markdown

#+begin_src emacs-lisp
  (use-package markdown-mode
    :ensure t)
#+end_src

** Java

#+begin_src emacs-lisp
  (use-package eglot-java
    :ensure t
    :defer t
    :config
    (setq eglot-java-eclipse-jdt-args `("-Xmx1G" "--add-modules=ALL-SYSTEM" "--add-opens"
                                        "java.base/java.util=ALL-UNNAMED" "--add-opens"
                                        "java.base/java.lang=ALL-UNNAMED"
                                        ,(concat "-javaagent:" (expand-file-name user-emacs-directory) "share/eclipse.jdt.ls/plugins/lombok.jar")
                                        ,(concat "-Xbootclasspath/a:" (expand-file-name user-emacs-directory) "share/eclips.jdtls/plugins/lombok.jar"))))

  (use-package java
    :ensure nil
    :defer t
    :config
    (defun rc/spring-run ()
      "Runs current spring boot project in an async shell window"
      (interactive)
      (let ((default-directory (project-root (project-current t))))
        (async-shell-command "mvn spring-boot:run" "\*Spring Boot\*")))

    (defun rc/spring-shell ()
      "Opens the current spring shell"
      (interactive)
      (if (get-buffer "\*Spring Boot\*")
          (display-buffer "\*Spring Boot\*")
        (message "No spring boot proccess running. Try spring-run."))))
#+end_src

** HTML

#+begin_src emacs-lisp
  (use-package mhtml-mode
    :ensure nil
    :defer t
    :preface
    (defun sgml-delete-tagged-text ()
      "Delete text between the tags that contain the current point"
      (interactive)
      (let ((b (point)))
        (sgml-skip-tag-backward 1)
        (when (not (eq b (point)))
          ;; moved somewhere, should be at front of a tag now
          (save-excursion 
            (forward-sexp 1)
            (setq b (point)))
          (sgml-skip-tag-forward 1)
          (backward-sexp 1)
          (delete-region b (point))
          (meow-insert))))
    :bind (("C-c C-i" . sgml-delete-tagged-text)))
    ;; :config
    ;; (define-key mhtml-mode-map (kbd "C-c C-i") 'sgml-delete-tagged-text))
#+end_src

** Tree-sitter

#+begin_src emacs-lisp
  (use-package toml-ts-mode
    :ensure nil
    :mode "\\.toml\\'")
#+end_src

** ~eglot~
#+begin_src emacs-lisp
  (use-package eglot
    :ensure nil
    :defer t
    :custom
    (eldoc-echo-area-use-multiline-p nil)
    ;; (eglot-ignored-server-capabilities '(:hoverProvider))
    :config
    (defun eglot-open-link ()
      "Open markdown link at point in the `eldoc-doc-buffer'."
      (interactive)
      (let ((url (get-text-property (point) 'help-echo)))
        (if url
            (browse-url-xdg-open url)
          (message "No URL found at point")))))
#+end_src

* Completion
:PROPERTIES:
:header-args+: :tangle lib/rca-completion.el
:END:

#+begin_src emacs-lisp
  (provide 'rca-completion)
#+end_src

** Completion general options

#+begin_src emacs-lisp
  (use-package emacs
    :init
    (setq tab-always-indent 'complete)
    (setq text-mode-ispell-word-completion nil))
#+end_src

** ~yasnippet~

#+begin_src emacs-lisp :tangle no
  (use-package yasnippet
    :ensure t
    :diminish yas-minor-mode
    :custom
    (yas-snippet-dirs `(,(locate-user-emacs-file "snippets")))
    :config
    (when (not (file-exists-p  snippets-directory))
      (make-directory snippets-directory))
    (yas-global-mode 1))
#+end_src

** ~tempel~

#+begin_src emacs-lisp
  ;; Configure Tempel
  (use-package tempel
    :ensure t
    ;; Require trigger prefix before template name when completing.
    ;; :custom
    ;; (tempel-trigger-prefix "<")

    :bind (("M-+" . tempel-complete) ;; Alternative tempel-expand
           ("M-*" . tempel-insert))

    :init

    ;; Setup completion at point
    (defun tempel-setup-capf ()
      ;; Add the Tempel Capf to `completion-at-point-functions'.
      ;; `tempel-expand' only triggers on exact matches. Alternatively use
      ;; `tempel-complete' if you want to see all matches, but then you
      ;; should also configure `tempel-trigger-prefix', such that Tempel
      ;; does not trigger too often when you don't expect it. NOTE: We add
      ;; `tempel-expand' *before* the main programming mode Capf, such
      ;; that it will be tried first.
      (setq-local completion-at-point-functions
                  (cons #'tempel-expand
                        completion-at-point-functions)))

    (add-hook 'conf-mode-hook 'tempel-setup-capf)
    (add-hook 'prog-mode-hook 'tempel-setup-capf)
    (add-hook 'text-mode-hook 'tempel-setup-capf)

    ;; Optionally make the Tempel templates available to Abbrev,
    ;; either locally or globally. `expand-abbrev' is bound to C-x '.
    ;; (add-hook 'prog-mode-hook #'tempel-abbrev-mode)
    ;; (global-tempel-abbrev-mode)
  )

  ;; Optional: Add tempel-collection.
  ;; The package is young and doesn't have comprehensive coverage.
  (use-package tempel-collection
    :ensure t)
#+end_src

** ~corfu~

#+begin_src emacs-lisp
  (use-package corfu
    :ensure t
    :bind
    (("C-<tab>" . completion-at-point)
     :map corfu-map
     ("S-SPC" . corfu-insert-separator)
     :map corfu-popupinfo-map
     ("M-n" . corfu-popupinfo-scroll-up)
     ("M-p" . corfu-popupinfo-scroll-down))
    :init
    (global-corfu-mode)
    :custom
    (corfu-min-width 70)
    (corfu-max-width 70)
    (corfu-popupinfo-mode 1)
    (corfu-popupinfo-delay '(1.0 . 1.2))
    (corfu-on-exact-match nil)
    ;; (corfu-auto-prefix 4)
    (corfu-separator ?\s)
    (corfu-auto t)
    (corfu-cycle t)
    (corfu-quit-no-match 'separator))
#+end_src

** Icons

#+begin_src emacs-lisp
  (use-package kind-icon
    :ensure t
    :after corfu
    :custom
    (kind-icon-use-icons nil)
    ;; (kind-icon-blend-background t)
    ;; (kind-icon-default-face 'corfu-default) ; only needed with blend-background
    :config
    (add-hook 'after-enable-theme-hook  #'kind-icon-reset-cache)
    (add-to-list 'corfu-margin-formatters #'kind-icon-margin-formatter))
#+end_src

** ~cape~

#+begin_src emacs-lisp
  (use-package cape
    :ensure t
    :init
    ;; Make dabbrev use the correct case
    (defun my-cape--dabbrev-fix-expansion (expansion)
      "Return the downcased EXPANSION.
   Removes trailing non-alphanumeric characters if present."
      (let ((downcased (downcase expansion)))
        (substring downcased 0 (string-match-p "[^[:alnum:]]+$" downcased))))
    
    (defun my-cape--dabbrev-list (input)
      "Find all dabbrev expansions for INPUT. "
      (cape--silent
        ;; Don't search all buffers. Only those with the same major-mode.
        (let ((dabbrev-check-other-buffers t)
              (dabbrev-check-all-buffers nil))
          (dabbrev--reset-global-variables))
        (cons
         (apply-partially #'string-prefix-p input)
         (cl-loop for w in (mapcar #'my-cape--dabbrev-fix-expansion
                                   (dabbrev--find-all-expansions input t))
                  if (>= (length w) cape-dabbrev-min-length) collect
                  (cape--case-replace t input w)))))

    (advice-add 'cape--dabbrev-list :override #'my-cape--dabbrev-list)

    (add-to-list 'completion-at-point-functions #'cape-dabbrev)
    (add-to-list 'completion-at-point-functions #'cape-keyword)
    (add-to-list 'completion-at-point-functions #'cape-file)
    (add-to-list 'completion-at-point-functions #'cape-elisp-block))
#+end_src

** ~dabbrev~

#+begin_src emacs-lisp
  (use-package dabbrev
    :bind (("C-." . dabbrev-expand)
           ("C-:" . dabbrev-completion))
    :config
    (add-to-list 'dabbrev-ignored-buffer-regexps "\\` ")
    ;; Since 29.1, use `dabbrev-ignored-buffer-regexps' on older.
    (add-to-list 'dabbrev-ignored-buffer-modes 'doc-view-mode)
    (add-to-list 'dabbrev-ignored-buffer-modes 'pdf-view-mode)
    (add-to-list 'dabbrev-ignored-buffer-modes 'tags-table-mode))
#+end_src

** ~smartparens~

#+begin_src emacs-lisp
  (use-package smartparens
    :ensure t
    :hook (prog-mode org-mode)
    :config
    (require 'smartparens-config))
#+end_src

** ~flyspell~

#+begin_src emacs-lisp :tangle no
    (use-package flyspell
      :bind (:map flyspell-mode-map
                  ("C-." . nil))
      :config
      (setq ispell-program-name "aspell"
    	ispell-personal-dictionary "~/.sync/dict/aspell_es"
    	ispell-dictionary "es")
      :hook (org-mode . flyspell-mode))
#+end_src

** ~jinx~

#+begin_src emacs-lisp
  (use-package jinx
    :ensure t
    :hook (org-mode . jinx-mode)
    :bind (("M-$" . jinx-correct)
           ("C-M-$" . jinx-languages))
    :config
    (setq jinx-languages "es en_US"))
#+end_src

** ~vundo~

#+begin_src emacs-lisp
  (use-package vundo
    :ensure t
    :bind ("C-x u" . vundo))
#+end_src

** ~eldoc~

#+begin_src emacs-lisp
  (use-package eldoc-box
    :ensure t
    :custom
    (eldoc-box-max-pixel-width 550)
    (eldoc-box-max-pixel-height 400)
    :bind (("M-√±" . eldoc-box-help-at-point)
           ("M-n" . eldoc-box-scroll-up)
           ("M-p" . eldoc-box-scroll-down)))
#+end_src

* Minibuffer
:PROPERTIES:
:header-args+: :tangle lib/rca-minibuffer.el
:END:

#+begin_src emacs-lisp
  (provide 'rca-minibuffer)
#+end_src

** ~vertico~

#+begin_src emacs-lisp
  (use-package vertico
    :ensure t
    :init
    (vertico-mode)
    :custom
    (vertico-cicle t)
    (vertico-count 12))
#+end_src

** ~vertico-posframe~

#+begin_src emacs-lisp
  (use-package vertico-posframe
    :ensure t
    :config
    (setq vertico-posframe-poshandler 'posframe-poshandler-frame-bottom-center)
    ;; (setq vertico-posframe-parameters
    ;;       '((left-fringe . 8)
    ;;         (right-fringe . 8)))
    (setq vertico-posframe-width 300)
    (vertico-posframe-mode 1))
#+end_src

** ~orderless~

#+begin_src emacs-lisp
  (use-package orderless
    :ensure t
    :custom
    (completion-styles '(orderless basic))
    (completion-category-overrides '((file (styles
                                            basic
                                            partial-completion)))))
#+end_src

** ~marginalia~

#+begin_src emacs-lisp
  (use-package marginalia
    :ensure t
    :config
    (marginalia-mode))
#+end_src

** ~consult~

#+begin_src emacs-lisp
  (use-package consult
    :ensure t
    :bind (("C-x C-b" . consult-buffer)
           ("C-x R" . consult-recent-file)
           ("C-x r i" . consult-register)
           ("C-x r b" . consult-bookmark)
           ("M-s f" . consult-recent-file)
           ("M-s b" . consult-bookmark)
           ("M-s l" . consult-line)
           :map org-mode-map
           ("M-s s" . consult-org-heading))
    :config
    (consult-customize consult-recent-file :preview-key nil)
    (consult-customize consult-bookmark :preview-key nil))
#+end_src

* Project management
:PROPERTIES:
:header-args+: :tangle lib/rca-project.el
:END:

#+begin_src emacs-lisp
  (provide 'rca-project)
#+end_src

** ~transient~

#+begin_src emacs-lisp
  (use-package transient
    :ensure t)
#+end_src

** ~llama~

#+begin_src emacs-lisp
  (use-package llama
    :ensure t)
#+end_src

** ~magit~

#+begin_src emacs-lisp
  (use-package magit
    :ensure t
    :requires transient
    :defer 1)
#+end_src

** ~skeletor~

#+begin_src emacs-lisp
  (use-package skeletor
    :ensure t
    :custom
    (skeletor-project-directory "~/Files/workspace/projects/")
    :config
    (skeletor-define-template "latex-article"
      :title "LaTeX Article"
      :no-license? t)
    (skeletor-define-template "LaTeX-APA7"
      :title "LaTeX APA 7th Article"
      :no-license? t)
    (skeletor-define-template "latex-beamer"
      :title "LaTeX Beamer"
      :no-license? t))
#+end_src

* Input
:PROPERTIES:
:header-args+: :tangle lib/rca-keyboard.el
:END:

#+begin_src emacs-lisp
  (provide 'rca-keyboard)
#+end_src

** ~meow~

#+begin_src emacs-lisp 
  (use-package meow
    :ensure t
    :config
    (defun meow-setup ()
      (setq meow-cheatsheet-layout meow-cheatsheet-layout-qwerty)
      (meow-motion-overwrite-define-key
       '("j" . meow-next)
       '("k" . meow-prev)
       '("<escape>" . ignore))

      (meow-leader-define-key
       ;; SPC j/k will run the original command in MOTION state.
       ;; '("j" . "H-j") ; Not needed anymore
       ;; '("k" . "H-k")
       '("d" . "C-x 0")
       '("t" . "C-x t")
       '("q" . "C-x C-k")
       '("r" . "C-x r")
       '("@" . "C-c @")
       '("u" . meow-universal-argument)
       ;; Use SPC (0-9) for digit arguments.
       '("1" . meow-digit-argument)
       '("2" . meow-digit-argument)
       '("3" . meow-digit-argument)
       '("4" . meow-digit-argument)
       '("5" . meow-digit-argument)
       '("6" . meow-digit-argument)
       '("7" . meow-digit-argument)
       '("8" . meow-digit-argument)
       '("9" . meow-digit-argument)
       '("0" . meow-digit-argument)
       '("/" . meow-keypad-describe-key)
       '("?" . meow-cheatsheet))

      (meow-normal-define-key
       '("0" . meow-expand-0)
       '("9" . meow-expand-9)
       '("8" . meow-expand-8)
       '("7" . meow-expand-7)
       '("6" . meow-expand-6)
       '("5" . meow-expand-5)
       '("4" . meow-expand-4)
       '("3" . meow-expand-3)
       '("2" . meow-expand-2)
       '("1" . meow-expand-1)
       '("-" . negative-argument)
       '(";" . meow-reverse)
       '("," . meow-inner-of-thing)
       '("." . meow-bounds-of-thing)
       '("[" . meow-beginning-of-thing)
       '("]" . meow-end-of-thing)
       '("a" . meow-append)
       '("A" . meow-open-below)
       '("b" . meow-back-word)
       '("B" . meow-back-symbol)
       '("c" . meow-change)
       '("d" . meow-delete)
       '("D" . meow-backward-delete)
       '("e" . meow-next-word)
       '("E" . meow-next-symbol)
       '("f" . meow-find)
       '("g" . meow-cancel-selection)
       '("G" . meow-grab)
       '("h" . meow-left)
       '("H" . meow-left-expand)
       '("i" . meow-insert)
       '("I" . meow-open-above)
       '("j" . meow-next)
       '("J" . meow-next-expand)
       '("k" . meow-prev)
       '("K" . meow-prev-expand)
       '("l" . meow-right)
       '("L" . meow-right-expand)
       '("m" . meow-join)
       '("n" . meow-search)
       '("o" . meow-block)
       '("O" . meow-to-block)
       '("p" . meow-yank)
       '("P" . meow-paren-mode)
       ;; '("q" . meow-quit)
       '("Q" . meow-indent)
       '("r" . meow-replace)
       '("R" . meow-swap-grab)
       '("s" . meow-kill)
       '("t" . meow-till)
       '("u" . meow-undo)
       '("U" . meow-undo-in-selection)
       '("v" . meow-visit)
       '("w" . meow-mark-word)
       '("W" . meow-mark-symbol)
       '("x" . meow-line)
       '("X" . meow-goto-line)
       '("¬∞" . meow-last-buffer)
       '("y" . meow-save)
       '("Y" . meow-sync-grab)
       '("z" . meow-pop-selection)
       '("¬ø" . repeat)
       '("<escape>" . ignore))

    (meow-motion-overwrite-define-key
     '("¬∞" . meow-last-buffer)
     '("j" . meow-next)
     '("k" . meow-prev)
     '("<escape>" . ignore))

    (with-eval-after-load 'consult
      (meow-normal-define-key
       '("X" . consult-goto-line)))


    (add-to-list 'meow-keypad-start-keys '(?z . ?z))
    ;; (add-to-list 'meow-keypad-start-keys '(?, . ?,)) ;; TODO Maybe use this for mode hotkeys

    (meow-thing-register 'angles
                         '(pair ("<") (">"))
                         '(pair ("<") (">")))
    ;; (meow-thing-register 'latex
    ;;                      latex-thing-regexp
    ;;                      latex-thing-regexp)
    (meow-thing-register 'inline-math
                         '(pair ("\\(") ("\\)"))
                         '(pair ("\\(") ("\\)")))
    (meow-thing-register 'display-math
                         '(pair ("\\[") ("\\]"))
                         '(pair ("\\[") ("\\]")))
    (meow-thing-register 'double-quotes
                         '(regexp "\"" "\"")
                         '(regexp "\"" "\""))
    (meow-thing-register 'single-quotes
                         '(regexp "\'" "\'")
                         '(regexp "\'" "\'"))
    ;; (meow-thing-register 'html-tag
    ;; 		       '(regexp "<.*>" "</?.*>")
    ;; 		       '(regexp "<.*>" "</?.*>"))
    ;; (add-to-list 'meow-char-thing-table '(?t . html-tag))
    (add-to-list 'meow-char-thing-table '(?a . angles))
    (add-to-list 'meow-char-thing-table '(?\" . double-quotes))
    (add-to-list 'meow-char-thing-table '(?x . latex))
    (add-to-list 'meow-char-thing-table '(?\' . single-quotes))
    (add-to-list 'meow-char-thing-table '(?m . inline-math))
    (add-to-list 'meow-char-thing-table '(?M . display-math))

    (setq meow-paren-keymap (make-keymap))

    (meow-define-state paren
      "paren state"
      :lighter " [P]"
      :keymap meow-paren-keymap)
    (setq meow-cursor-type-paren 'hollow)

    (defun wrap-string () (interactive) (sp-wrap-with-pair "\""))
    (defun back-transpose () (interactive) (sp-transpose-sexp -1))

    (meow-define-keys 'paren
      '("<escape>" . meow-normal-mode)
      '("e" . sp-forward-sexp)
      '("b" . sp-backward-sexp)
      ;; '("j" . sp-down-sexp)
      ;; '("k" . sp-up-sexp)
      '("o s" . sp-wrap-square)
      '("o r" . sp-wrap-round)
      '("o c" . sp-wrap-curly)
      '("o \"" . wrap-string)
      '("O" . sp-unwrap-sexp)
      '("z" . meow-pop-selection)
      '("u" . meow-undo)
      '("U" . undo-redo))

    (meow-normal-define-key
     '("P" . meow-paren-mode)))

    (meow-setup)
    ;; (when (featurep 'corfu)
    ;;   (add-hook 'meow-insert-exit-hook 'corfu-quit))
    (meow-global-mode 1))
#+end_src
    
* Tex
:PROPERTIES:
:header-args+: :tangle lib/rca-tex.el
:END:

#+begin_src emacs-lisp
  (provide 'rca-tex)

  (defvar rc/latex-subdir-plural
    '("figure" "table" "image" "section")
    "List of latex filetypes which need a plural form")

  (defun rc/is-main-latex-file ()
    "Returns t if the current file is the main tex file, nil otherwise"
    (when (equal (file-name-base buffer-file-name) "main") t))

  (defun rc/latex-file-subdirectory (filetype)
    "Define the subdirectory in a latex project for the filetype submitted as
  input"
    (let* ((file-path-prefix
            (if (rc/is-main-latex-file) "./" "../"))
           (file-type-subdir
            (if (member filetype rc/latex-subdir-plural)
                (concat filetype "s/")
              (concat filetype "/")))
           (file-type-subdir-with-prefix
            (concat file-path-prefix file-type-subdir))
           (file-path
            (read-file-name "File: " file-type-subdir-with-prefix "" t))
           (file-relative-path
            (replace-regexp-in-string
             (concat ".*" file-type-subdir "\\(.*\\)")
             (concat file-type-subdir-with-prefix "\\1") file-path)))
      (format "%s" file-relative-path)))

  (defun rc/latex-insert-file (&optional filetype)
    "Insert the relative path to a latex extra file in a subdirectory"
    (interactive "P")
    (if filetype
        (let ((filepath
               (rc/latex-file-subdirectory filetype)))
          (insert filepath))
      (let* ((filetype
              (completing-read "File type: "
                               '("image" "figure" "table" "code" "section") nil t))
             (filepath
              (rc/latex-file-subdirectory filetype)))
        (insert filepath))))

  (defun rc/cdlatex-pos-cursor-insert-file (&optional filetype)
    "Function to use in cdlatex command completion"
    (cdlatex-position-cursor)
    (if filetype
        (rc/latex-insert-file filetype)
      (let ((filetype (completing-read
                       "File type: " '("figure" "table" "section") nil t)))
        (rc/latex-insert-file filetype))))

  (defun rc/latex-array-separation ()
    (when (line-contains? "&")
      (progn
        (replace-regexp-in-line "&" " & ")
        (LaTeX-indent-line)
        (beginning-of-line-text)
        (left-char 1))))

  (advice-add 'LaTeX-insert-item :after #'rc/latex-array-separation)
#+end_src

** ~auctex~

#+begin_src emacs-lisp
  (use-package tex
    :ensure auctex
    :after pdf-tools
    :preface
    (defun rc/latex-init ()
      "Defines what modes are activated by default when entering AuCtex mode"
      (prettify-symbols-mode)
      (turn-on-cdlatex)
      (outline-minor-mode)  
      ;; (rc/auctex-macros)
      (TeX-source-correlate-mode t)
      (tex-fold-mode 1)
      (TeX-PDF-mode t)
      (reftex-mode t)
      (LaTeX-math-mode t))
    :init
    ;; Correct way to call hooks for auctex
    (add-hook 'LaTeX-mode-hook 'rc/latex-init)
    (setopt
     TeX-fold-macro-spec-list
     '(("{1}" ("emph")) ("{1}" ("textbf"))
       ("{1}" ("textit")) ("[1]:||‚ñ∫" ("item"))
       ("¬ß {1}" ("section" "section*"))
       ("[f]‚Üí‚Äñ{1}‚Äñ" ("footnote" "marginpar"))
       ("[c]‚Üí‚Äñ{1}‚Äñ" ("cite")) ("[l]‚Üí‚Äñ{1}‚Äñ" ("label"))
       ("[r]‚Üí‚Äñ{1}‚Äñ" ("ref" "pageref" "eqref" "footref"))
       ("[i]‚Üí‚Äñ{1}‚Äñ" ("index" "glossary"))
       ("¬ß¬ß {1}" ("subsection" "subsection*"))
       ("¬ß¬ß¬ß {1}" ("subsubsection" "subsubsection*"))
       ("¬∂¬∂ {1}" ("subparagraph" "subparagraph*"))
       ("¬∂ {1}" ("paragraph" "paragraph*"))))
    :custom
    (TeX-parse-self t "Enable parse on load")
    (TeX-auto-save t "Enable parse on save")
    (TeX-arg-input-file-search 'nil "Find file manually")
    :config
    (setq-default preview-scale 1.4
                  prettify-symbols-unprettify-at-point 'right-edge
                  preview-scale-function (lambda () (* (/ 10.0 (preview-document-pt)) preview-scale))
                  TeX-source-correlate-method 'synctex
                  TeX-source-correlate-start-server t
                  TeX-master nil
                  TeX-view-program-selection '((output-pdf "PDF Tools"))))
#+end_src

** ~cdlatex~

#+begin_src emacs-lisp
  (use-package cdlatex
    :ensure t
    :defer t
    :init
    (defvar rc/cdlatex-env-list
      '(("axiom" "\\begin{axiom}\nLABEL\n?\n\\end{axiom}\n" nil)
        ("theorem" "\\begin{theorem}\nLABEL\n?\n\\end{theorem}\n" nil))
      "cdlatex enviroments")
    (defvar rc/cdlatex-command-list
      '(
        ;; ("ref"
        ;;  "Insert a new reference"
        ;;  "" consult-reftex-insert-reference nil t nil)
        ("gph"
         "Insert an image"
         "\\includegraphics[width=0.6\\linewidth]{?}"
         rc/cdlatex-pos-cursor-insert-file ("image") t nil)
        ("inp"
         "Input a file"
         "\\input{?}"
         rc/cdlatex-pos-cursor-insert-file nil t nil)
        ("inc"
         "Include a file"
         "\\include{?}"
         rc/cdlatex-pos-cursor-insert-file nil t nil)
        ("dm"
         "Insert a math display block"
         "\\[ ? \\]" cdlatex-position-cursor nil t nil)
        ("mm"
         "Insert an inline math block"
         "\\( ? \\)" cdlatex-position-cursor nil t nil)
        ("int"
         "Insert simple integral"
         "\\int_{?}" cdlatex-position-cursor nil nil t)
        ("oint"
         "Insert closed integral"
         "\\oint_{?}" cdlatex-position-cursor nil nil t)
        ("dv"
         "Insert a spaced differential variable"
         "\\, d?" cdlatex-position-cursor nil nil t)
        ("t."
         "Insert therefore symbol"
         "\\therefore" cdlatex-position-cursor nil nil t)
        ("intd"
         "Insert a definite integral limits"
         "\\biggr\\vert_{?}^{}" cdlatex-position-cursor nil nil t)
        ("int2"
         "Insert a definite integral limits"
         "\\iint" cdlatex-position-cursor nil nil t)
        ("int3"
         "Insert a definite integral limits"
         "\\iiint" cdlatex-position-cursor nil nil t)
        ("sci"
         "Insert scientific notation"
         "\\times 10^{?}" cdlatex-position-cursor nil nil t))
      "cdlatex custom commands")
    (setq cdlatex-env-alist rc/cdlatex-env-list
          cdlatex-command-alist rc/cdlatex-command-list)
    :custom
    (cdlatex-paired-parens "$([{")
    (cdlatex-math-modify-alist '((66 "\\mathbb" nil t nil nil)))
    :bind ( :map cdlatex-mode-map
            ("C-<return>" . nil)
            ("¬¥" . cdlatex-math-symbol)
            ("<tab>" . cdlatex-tab)))
#+end_src

** ~pdf-tools~

#+begin_src emacs-lisp
  (use-package pdf-tools
    :ensure t
    ;; :defer t
    :mode ("\\.pdf\\'" . pdf-view-mode)
    :hook ((pdf-view-mode . pdf-links-minor-mode)
           (pdf-view-mode . pdf-view-themed-minor-mode)
           (pdf-view-mode . pdf-sync-minor-mode))
    :init
    (pdf-tools-install)
    :custom
    (pdf-view-display-size 'fit-page "Fit to page by default")
    (pdf-annot-activate-created-annotations t "Activate annotations")
    :config
    (defvar mode-line-format--old nil
      "Variable to store last mode line format to restore it
  when deactivating presentation-mode")

    (define-minor-mode presentation-mode
      "Remove visual elements for presentation"
      :global nil
      (if presentation-mode
          (progn
            (setq mode-line-format--old mode-line-format)
            (setq mode-line-format nil)
            (tab-bar-mode -1))
        (setq mode-line-format mode-line-format--old)
        (tab-bar-mode)))
    (define-key pdf-view-mode-map (kbd "<f5>") 'presentation-mode)
    (add-hook 'TeX-after-compilation-finished-functions #'TeX-revert-document-buffer)
    (define-key pdf-view-mode-map (kbd "C-s") 'isearch-forward)
    (define-key pdf-view-mode-map (kbd "C-r") 'isearch-backward))
#+end_src

* Tools
:PROPERTIES:
:header-args+: :tangle lib/rca-tools.el
:END:

#+begin_src emacs-lisp
  (provide 'rca-tools)
#+end_src

** ~ebuku~

#+begin_src emacs-lisp
  (use-package ebuku
    :ensure t
    :defer t
    :bind ("C-z b" . ebuku)
    :custom-face
    (ebuku-tags-face ((t (:inherit font-lock-keyword-face))))
    (ebuku-title-face ((t (:inherit font-lock-constant-face))))
    :custom
    (ebuku-results-limit 25))
#+end_src

** ~denote~

#+begin_src emacs-lisp
  (use-package denote
    :ensure t
    :custom
    (denote-known-keywords '("material" "examen" "ajeno" "practica" "apuntes"))
    (denote-directory "~/.sync/archive/notes")
    (denote-dired-directories '("~/.sync/archive/notes" "~/.sync/archive/journal" "/home/rcaled/Files/Downloads/universidad"))
    :config
    (add-hook 'dired-mode-hook 'denote-dired-mode-in-directories))

  (use-package denote-journal
    :ensure t
    :custom
    (denote-journal-title-format 'day-date-month-year)
    (denote-journal-directory "~/.sync/archive/journal"))

  (use-package denote-search
    :ensure t
    :defer t)
#+end_src

** ~embark~

#+begin_src emacs-lisp
  (use-package embark
    :ensure t
    :bind (("C-c o" . embark-act)
           :map embark-general-map
           ("G" . +embark-google-search))
    :init
    (defun +embark-google-search (term)
      (interactive "sSearch Term: ")
      (browse-url (format "https://google.com/search?q=%s" term)))
    :config
    (add-to-list 'display-buffer-alist '("\\*Embark Actions\\*" (display-buffer-pop-up-window))))

  (use-package embark-consult
    :ensure t)
#+end_src

** ~citar~

#+begin_src emacs-lisp
  (use-package citar
    :ensure t
    :bind (("C-z c o" . citar-open)
           ("C-z c c" . citar-insert-citation)
           ("C-z c r" . citar-insert-reference)
           ("C-z c b" . citar-insert-bibtex)
           ("C-z c k" . citar-insert-keys))
    :custom
    (org-cite-insert-processor 'citar)
    (org-cite-follow-processor 'citar)
    (org-cite-activate-processor 'citar)
    :config
    (let ((documents-path (xdg-user-dir "DOCUMENTS"))
          (archive-path "~/.sync/archive/"))
      (setq org-cite-global-bibliography
            (mapcar (lambda (entry) (concat archive-path entry)) '("articles.bib" "books.bib")))
      (setq citar-library-paths
            (mapcar (lambda (entry) (concat documents-path entry)) '("/library/articles/" "/library/books/"))
            citar-notes-paths (list (concat archive-path "bibnotes/")))
      (setq citar-file-note-extensions '("org")
            citar-library-file-extensions '("pdf")
            citar-bibliography org-cite-global-bibliography))
    (defcustom +citar-remote-library-path nil "Remote path that contains the library"
      :type '(string))
    (defcustom +citar-library-server nil "Remote library ssh server"
      :type '(string))
    (defcustom +citar-library-port nil "Remote library ssh server port"
      :type '(string))
    (defcustom +citar-local-library-path nil "Local path that contains the library"
      :type '(string))
    (defun +citar-open-file-externally (citekey)
      "Opens associated file in the default system reader"
      (let ((file (car (car (hash-table-values (citar-get-files citekey))))))
        (if file (start-process "open pdf" nil "xdg-open" file)
          (message "No pdf file found with this citekey"))))
    (defun +citar-scholar-search (citekey)
      "Search the entry in google scholar"
      (browse-url (format "https://scholar.google.com/scholar?q=%s"
                          (citar-get-value "title" citekey))))
    (defun +citar-download-file (citekey)
      "Downloads file from the remote server and stores it in the library"
      (start-process "citar-download-file" nil "download-from-archive"
                     +citar-library-server +citar-library-port
                     +citar-remote-library-path citekey +citar-local-library-path)))

  (use-package citar-embark
    :ensure t
    :diminish
    :after (citar embark)
    :defer nil
    :bind (:map citar-embark-map
                ("g" . +citar-scholar-search)
                ("d" . +citar-download-file)
                ("F" . +citar-open-file-externally)
                :map citar-embark-citation-map
                ("g" . +citar-scholar-search)
                ("d" . +citar-download-file)
                ("F" . +citar-open-file-externally))
    :config
    (citar-embark-mode))
#+end_src

** ~ebib~

#+begin_src emacs-lisp
  (use-package ebib
    :ensure t
    :defer t
    :custom
    (ebib-preload-bib-files '("~/.sync/archive/articles.bib" "~/.sync/archive/books.bib"))
    (ebib-file-search-dirs '("~/Files/Documents/library/articles" "~/Files/Documents/library/books"))
    (ebib-file-associations '(("ps" . "gv")))
    :config
    (define-key ebib-index-mode-map (kbd "O") '+ebib-open-file-externally)
    (define-key ebib-index-mode-map (kbd "L") '+ebib-scholar-search)
    (defun +ebib-open-file-externally () ; Maybe I can define args?
      (interactive)
      (let ((file (ebib--expand-file-name (ebib--select-file nil 1 (ebib--get-key-at-point)))))
        (if file (start-process "open pdf" nil "xdg-open" file)
          (message "No PDF file found with this citekey"))))
    (defun +ebib-scholar-search ()
      (interactive)
      (browse-url (format "https://scholar.google.com/scholar?q=%s"
                          (ebib-get-field-value "title" (ebib--get-key-at-point) ebib--cur-db nil t)))))
#+end_src

** ~alert~

#+begin_src emacs-lisp
  (use-package alert
    :ensure t
    :config
    (defun rc/alert-notifications-notify (info)
      "Show the alert defined by INFO with `notifications-notify'."
      (let ((id (notifications-notify :title "Reminder"
                                      :body  (plist-get info :message)
                                      :app-icon (plist-get info :icon)
                                      :app-name "emacs"
                                      :timeout (if (plist-get info :persistent) 0 -1)
                                      :replaces-id (gethash (plist-get info :id) alert-notifications-ids)
                                      :urgency (cdr (assq (plist-get info :severity)
                                                          alert-notifications-priorities))
                                      :actions '("default" "Open corresponding buffer")
                                      :on-action (lambda (id action)
                                                   (when (string= action "default")
                                                     (switch-to-buffer (plist-get info :buffer)))))))
        (when (plist-get info :id)
          (puthash (plist-get info :id) id alert-notifications-ids)))
      (alert-message-notify info))
    (alert-define-style 'rc-style :title "My custom style" :notifier 'rc/alert-notifications-notify)
    (setq alert-default-style 'rc-style)

    ;; Short reminder commands
    (defun short-reminder (time message)
      (interactive "nMinutes: \nsMessage: ")
      (run-at-time (* (/ (float time) 2) 60) nil #'alert (concat message " (Timer at half)"))
      (run-at-time (* time 60) nil #'alert message)))

  (use-package org-wild-notifier
    :ensure t
    :requires alert
    :config
    (setq org-wild-notifier-alert-time '(4320 2880 1440 720 360 180 120 60 15 5 1))
    ;; (setq org-wild-notifier-keyword-whitelist nil)
    (org-wild-notifier-mode))
#+end_src

* Local variables
# Local Variables:
# eval: (add-hook 'after-save-hook (lambda ()(org-babel-tangle)) nil t)
# End:

